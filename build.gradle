import io.cloudflight.gradle.RepositoryVisibility

plugins {
    id "cloudflight-gradle-plugin" version "6.4.0"
}

description "Jems standard plugins"
group 'io.cloudflight.jems.plugin.standard'
version '1.1.16'

cloudflight {
    visibility = RepositoryVisibility.PUBLIC
    softwareProjectKey = "interact/ems"
}

if (isCloudflightBuild()) {
    bootJar {
        enabled = false
    }
}

configurations {
    provided
    implementation.extendsFrom provided
}

jar {
    enabled = true
    from { (configurations.compileClasspath - configurations.provided).collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

if (isKotlinProject(project)) {
    dependencies {
        provided platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
        annotationProcessor platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
        provided platform("io.cloudflight.platform:platform-test-bom:$cloudflightPlatformVersion")
        kapt platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")

        constraints {
            provided('io.cloudflight.jems.plugin.contract:jems-plugin-contract:1.1.11')
            implementation('com.opencsv:opencsv:5.5.2')
            implementation("com.openhtmltopdf:openhtmltopdf-core:1.0.10")
            implementation("com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10")
            implementation("com.openhtmltopdf:openhtmltopdf-java2d:1.0.10")
            implementation("com.openhtmltopdf:openhtmltopdf-rtl-support:1.0.10")
            implementation("com.openhtmltopdf:openhtmltopdf-svg-support:1.0.10")
            implementation("org.apache.poi:poi:5.2.0")
            implementation("org.apache.poi:poi-ooxml:5.2.0")
        }
    }
}
dependencies {
    // load for local development (if projects are cloned in same root as jems)
    // implementation files('../jems-plugin-interface/build/libs/jems-plugin-contract-1.1.10-SNAPSHOT.jar')
    provided('io.cloudflight.jems.plugin.contract:jems-plugin-contract')
    provided('org.springframework.boot:spring-boot-starter-web')
    provided('org.springframework.boot:spring-boot-starter-thymeleaf')

    implementation('com.openhtmltopdf:openhtmltopdf-core')
    implementation('com.openhtmltopdf:openhtmltopdf-java2d')
    implementation('com.openhtmltopdf:openhtmltopdf-pdfbox')
    implementation('com.openhtmltopdf:openhtmltopdf-rtl-support')
    implementation('com.openhtmltopdf:openhtmltopdf-svg-support')
    implementation('com.opencsv:opencsv')
    // used in svg dependency otherwise commons-io:1.3.1 would interfere with the one needed for jems-server
    implementation('commons-io:commons-io:2.8.0')

    implementation('org.apache.poi:poi')
    implementation('org.apache.poi:poi-ooxml')
}

