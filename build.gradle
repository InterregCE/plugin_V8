import io.cloudflight.gradle.RepositoryVisibility

plugins {
    id "cloudflight-gradle-plugin" version "5.15.2"
}

description "Jems standard plugins"
group 'io.cloudflight.jems.plugin.standard'
version '1.0.25'

cloudflight {
    visibility = RepositoryVisibility.PUBLIC
    softwareProjectKey = "interact/ems"
}

if (isCloudflightBuild()) {
    bootJar {
        enabled = false
    }
}

configurations {
    provided
    implementation.extendsFrom provided
}

jar {
    enabled = true
    from { (configurations.compileClasspath - configurations.provided).collect { it.isDirectory() ? it : zipTree(it) } }
}

if (isKotlinProject(project)) {
    dependencies {
        provided platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
        annotationProcessor platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")
        provided platform("io.cloudflight.platform:platform-test-bom:$cloudflightPlatformVersion")
        kapt platform("io.cloudflight.platform:platform-bom:$cloudflightPlatformVersion")

        constraints {
            provided('io.cloudflight.jems.plugin.contract:jems-plugin-contract:1.0.28')
            implementation('com.opencsv:opencsv:5.5.2')
            implementation("com.openhtmltopdf:openhtmltopdf-core:1.0.10")
            implementation("com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10")
            implementation("com.openhtmltopdf:openhtmltopdf-java2d:1.0.10")
            implementation("com.openhtmltopdf:openhtmltopdf-rtl-support:1.0.10")
        }
    }
}
dependencies {
    // load for local development (if projects are cloned in same root as jems)
    // implementation files('../jems-plugin-contract/build/libs/jems-plugin-contract-1.0.28-SNAPSHOT.jar')
    provided('io.cloudflight.jems.plugin.contract:jems-plugin-contract')
    provided('org.springframework.boot:spring-boot-starter-web')
    provided('org.springframework.boot:spring-boot-starter-thymeleaf')

    implementation('com.openhtmltopdf:openhtmltopdf-core')
    implementation('com.openhtmltopdf:openhtmltopdf-java2d')
    implementation('com.openhtmltopdf:openhtmltopdf-pdfbox')
    implementation('com.openhtmltopdf:openhtmltopdf-rtl-support')
    implementation('com.opencsv:opencsv')
}

